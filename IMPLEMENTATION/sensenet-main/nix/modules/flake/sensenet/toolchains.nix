# nix/modules/flake/buck2/toolchain.nix
#
# Generate .buckconfig.local content for a Buck2 project.
#
{ lib, pkgs }:
{
  # Generate buckconfig.local INI content from toolchain config
  mkBuckconfigLocal =
    {
      cxx ? null,
      haskell ? null,
      rust ? null,
      lean ? null,
      python ? null,
      nv ? null,
      purescript ? null,
      remoteExecution ? null,
      extra ? "",
    }:
    let
      sections = lib.filter (s: s != null && s != "") [
        cxx
        haskell
        rust
        lean
        python
        nv
        purescript
        remoteExecution
        extra
      ];
    in
    ''
      #                                                                      // auto
      #                                                         // do // not // edit
      ${lib.concatStringsSep "\n" sections}
    '';

  # C++ toolchain section
  mkCxxSection =
    { llvmPackages }:
    let
      llvm = llvmPackages;
      inherit (pkgs) gcc;
      gcc-unwrapped = gcc.cc;
      gcc-version = gcc-unwrapped.version;
      triple = pkgs.stdenv.hostPlatform.config;
    in
    ''

      [cxx]
      cc = ${llvm.clang}/bin/clang
      cxx = ${llvm.clang}/bin/clang++
      cpp = ${llvm.clang}/bin/clang-cpp
      ar = ${llvm.lld}/bin/llvm-ar
      ld = ${llvm.lld}/bin/ld.lld
      clang_resource_dir = ${llvm.clang}/resource-root
      gcc_include = ${gcc-unwrapped}/include/c++/${gcc-version}
      gcc_include_arch = ${gcc-unwrapped}/include/c++/${gcc-version}/${triple}
      glibc_include = ${pkgs.glibc.dev}/include
      gcc_lib = ${gcc-unwrapped}/lib/gcc/${triple}/${gcc-version}
      gcc_lib_base = ${gcc.cc.lib}/lib
      glibc_lib = ${pkgs.glibc}/lib
    '';

  # Haskell toolchain section
  mkHaskellSection =
    { ghc, ghcVersion }:
    ''

      [haskell]
      ghc = ${ghc}/bin/ghc
      ghc_pkg = ${ghc}/bin/ghc-pkg
      haddock = ${ghc}/bin/haddock
      ghc_version = ${ghcVersion}
      ghc_lib_dir = ${ghc}/lib/ghc-${ghcVersion}/lib
      global_package_db = ${ghc}/lib/ghc-${ghcVersion}/lib/package.conf.d
    '';

  # Rust toolchain section
  mkRustSection = _: ''

    [rust]
    rustc = ${pkgs.rustc}/bin/rustc
    rustdoc = ${pkgs.rustc}/bin/rustdoc
    clippy_driver = ${pkgs.clippy}/bin/clippy-driver
    cargo = ${pkgs.cargo}/bin/cargo
  '';

  # Lean toolchain section
  mkLeanSection =
    _:
    let
      lean = pkgs.lean4;
    in
    ''

      [lean]
      lean = ${lean}/bin/lean
      leanc = ${lean}/bin/leanc
      lean_lib_dir = ${lean}/lib/lean/library
      lean_include_dir = ${lean}/include
    '';

  # Python toolchain section
  mkPythonSection =
    { python, pybind11 ? null }:
    let
      # Handle both plain python and python.withPackages results
      # withPackages returns an env that wraps the original python
      pythonPkg = python.passthru.pythonOnBuildForHost or python.passthru.python or python;
      pyVersion = lib.versions.majorMinor pythonPkg.version;
      pybind11Section = lib.optionalString (pybind11 != null) ''
        pybind11_include = ${pybind11}/include
      '';
    in
    ''

      [python]
      # Python toolchain from Nix
      interpreter = ${python}/bin/python3
      python_include = ${python}/include/python${pyVersion}
      ${pybind11Section}'';

  #                                                                    // nvidia
  mkNvSection =
    {
      nvidia-sdk,
      clang-unwrapped,
      mdspan,
    }:
    ''

      [nv]
      nvidia_sdk_path = ${nvidia-sdk}
      nvidia_sdk_include = ${nvidia-sdk}/include
      nvidia_sdk_lib = ${nvidia-sdk}/lib
      ptxas = ${nvidia-sdk}/bin/ptxas
      fatbinary = ${nvidia-sdk}/bin/fatbinary
      archs = sm_90
      # Use unwrapped clang for CUDA (avoids NixOS hardening flags like -fzero-call-used-regs)
      clang = ${clang-unwrapped}/bin/clang++
      # mdspan for device code (Kokkos reference implementation)
      mdspan_include = ${mdspan}/include
    '';

  # PureScript toolchain section
  mkPureScriptSection =
    _:
    ''

      [purescript]
      purs = ${pkgs.purescript}/bin/purs
      spago = ${pkgs.spago}/bin/spago
      node = ${pkgs.nodejs}/bin/node
    '';

  # Remote Execution section (NativeLink)
  mkRemoteExecutionSection =
    {
      scheduler,
      schedulerPort,
      cas,
      casPort,
      tls,
      instanceName,
    }:
    let
      tlsStr = if tls then "true" else "false";
    in
    ''

      # ────────────────────────────────────────────────────────────────────────────
      # NativeLink Remote Execution
      # ────────────────────────────────────────────────────────────────────────────

      [build]
      execution_platforms = toolchains//:lre

      [buck2_re_client]
      engine_address = grpc://${scheduler}:${toString schedulerPort}
      cas_address = grpc://${cas}:${toString casPort}
      action_cache_address = grpc://${cas}:${toString casPort}
      tls = ${tlsStr}
      instance_name = ${instanceName}

      [buck2_re_client.platform_properties]
      OSFamily = linux
      container-image = nix-worker
    '';
}
