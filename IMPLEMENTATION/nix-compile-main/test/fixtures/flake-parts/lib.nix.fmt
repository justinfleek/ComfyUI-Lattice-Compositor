{ lib
  # Optionally a string with extra version info to be included in the error message
  # in case is lib is out of date. Empty or starts with space.
, revInfo ? ""
}:
let
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  # :: a
  inherit (lib)
    mkOption
    mkOptionType
    defaultFunctor
    isAttrs
    isFunction
    showOption
    throwIf
    types
    warnIf
    getAttrFromPath
    setAttrByPath
    attrByPath
    optionalAttrs
    ;
  # :: a
  inherit (lib.modules)
    mkAliasAndWrapDefsWithPriority;
  # :: a
  # :: a
  inherit (lib.types)
    path
    submoduleWith
    ;

  # Polyfill isFlake until Nix with https://github.com/NixOS/nix/pull/7207 is common
  # :: a -> Bool
  isFlake = maybeFlake:
    if maybeFlake ? _type
    then maybeFlake._type == "flake"
    else maybeFlake ? inputs && maybeFlake ? outputs && maybeFlake ? sourceInfo;

  /**
    Deprecated for any use except type-merging into `perSystem`.
    Use `lib.types.deferredModuleWith` instead, and add `apply = m: [ m ];` if needed.

    The deferredModule type was pioneered in flake-parts for the `perSystem` option.
    The Nixpkgs version has an improved merge function that returns a single module,
    whereas this version returns a list. The flake-parts version was not updated to
    match this improvement in Nixpkgs.

    # History

    This predates `lib.types.deferredModuleWith`, added in Nixpkgs 22.11
    (https://github.com/NixOS/nixpkgs/pull/163617).
    Documented as deprecated in flake-parts in January 2026.
  */
  # :: { staticModules? : [Any] } -> a
  deferredModuleWith =
    attrs@{ staticModules ? [ ] }: mkOptionType {
      # :: "deferredModule"
      name = "deferredModule";
      # :: "module"
      description = "module";
      # :: a -> Bool
      check = x: isAttrs x || isFunction x || path.check x;
      # :: a -> [Any] -> [Any]
      merge = loc: defs: staticModules ++ map (def: lib.setDefaultModuleLocation "${def.file}, via option ${showOption loc}" def.value) defs;
      # :: [Any]
      # :: [Any]
      inherit (submoduleWith { modules = staticModules; })
        getSubOptions
        getSubModules;
      # :: a -> b
      substSubModules = m: deferredModuleWith (attrs // {
        # :: a
        staticModules = m;
      });
      # :: { binOp : c -> d -> { staticModules : e }, payload : { staticModules : [Any] }, type : { staticModules : a } -> b }
      functor = defaultFunctor "deferredModuleWith" // {
        # :: { staticModules : a } -> b
        type = deferredModuleWith;
        # :: { staticModules : [Any] }
        payload = {
          inherit staticModules;
        };
        # :: a -> b -> { staticModules : c }
        binOp = lhs: rhs: {
          # :: a
          staticModules = lhs.staticModules ++ rhs.staticModules;
        };
      };
    };

  # Internal: preserves legacy list-merge behavior for perSystem type-merging.
  # :: Any -> a
  mkLegacyDeferredModuleType =
    module:
    deferredModuleWith {
      # :: [a]
      staticModules = [ module ];
    };

  # :: String
  errorExample = ''
    For example:

        outputs = inputs@{ flake-parts, ... }:
          flake-parts.lib.mkFlake { inherit inputs; } { /* module */ };

    To avoid an infinite recursion, *DO NOT* pass `self.inputs` and
    *DO NOT* pass `inherit (self) inputs`, but pass the output function
    arguments as `inputs` like above.
  '';

  # :: { attrsWith : n, defaultModule : d -> d, evalFlakeModule : { inputs? : a, moduleLocation? : String, self? : b, specialArgs? : {} } -> c, importAndPublish : t2 -> Path -> { lib : t3 } | ... -> { _class : "flake", imports : [Path] }, importApply : Path -> y -> z, memoizeStr : t1, mkAliasOptionModule : s -> t -> { config : u, options : v } | ... -> { config : x, options : w }, mkDeferredModuleOption : l -> m, mkDeferredModuleType : Any -> j, mkFlake : e -> f -> g, mkPerSystemOption : l -> m, mkPerSystemType : Any -> k, mkSubmoduleOptions : h -> i, mkTransposedPerSystemModule : { file : q, name : o, option : p } -> { _file : q, config : {}, options : { perSystem : r } } }
  flake-parts-lib = rec {
    # :: { inputs? : a, moduleLocation? : String, self? : b, specialArgs? : {} } -> c
    evalFlakeModule =
      args@
      { inputs ? self.inputs
      , specialArgs ? { }

        # legacy
      , self ? inputs.self or (throw ''
          When invoking flake-parts, you must pass all the flake output arguments,
          and not just `self.inputs`.

          ${errorExample}
        '')
      , moduleLocation ? "${self.outPath}/flake.nix"
      }:
      let
        # :: a
        inputsPos = builtins.unsafeGetAttrPos "inputs" args;
        # :: String
        errorLocation =
          # Best case: user makes it explicit
          args.moduleLocation or (
            # Slightly worse: Nix does not technically commit to unsafeGetAttrPos semantics
            if inputsPos != null
            then inputsPos.file
            # Slightly worse: self may not be valid when an error occurs
            else if args?inputs.self.outPath
            then args.inputs.self.outPath + "/flake.nix"
            # Fallback
            else "<mkFlake argument>"
          );
      in
      throwIf
        (!args?self && !args?inputs) ''
        When invoking flake-parts, you must pass in the flake output arguments.

        ${errorExample}
      ''
        warnIf
        (!args?inputs) ''
        When invoking flake-parts, it is recommended to pass all the flake output
        arguments in the `inputs` parameter. If you only pass `self`, it's not
        possible to use the `inputs` module argument in the module `imports`.

        Please pass the output function arguments. ${errorExample}
      ''

        (module:
        lib.evalModules {
          # :: { flake-parts-lib : a, inputs : b, moduleLocation : String, self : c }
          specialArgs = {
            inherit self flake-parts-lib moduleLocation;
            # :: a
            inputs = args.inputs or /* legacy, warned above */ self.inputs;
          } // specialArgs;
          # :: [Path]
          modules = [ ./all-modules.nix (lib.setDefaultModuleLocation errorLocation module) ];
          # :: "flake"
          class = "flake";
        }
        );

    # Function to extract the default flakeModule from
    # what may be a flake, returning the argument unmodified
    # if it's not a flake.
    #
    # Useful to map over an 'imports' list to make it less
    # verbose in the common case.
    # :: a -> a
    defaultModule = maybeFlake:
      if isFlake maybeFlake
      then maybeFlake.flakeModules.default or maybeFlake
      else maybeFlake;

    # :: a -> b -> c
    mkFlake = args: module:
      let
        # :: a
        eval = flake-parts-lib.evalFlakeModule args module;
      in
      eval.config.flake;

    /**
      Deprecated. Declare options directly, e.g. `options.foo.bar = mkOption { ... }`,
      provided that `foo` is already declared as a submodule option.

      In flake-parts, `flake` is declared as a submodule option by the core modules,
      so `options.flake.<name>` declarations work directly.

      This function wraps option declarations in a submodule, allowing them to
      be merged into an existing submodule option. For example, if `foo` is
      already declared as a submodule option, using
      `options.foo = mkSubmoduleOptions { bar = mkOption {...}; }` would add
      `bar` to the `foo` submodule.

      # History

      This was a workaround for https://github.com/NixOS/nixpkgs/issues/146882,
      fixed in Nixpkgs 22.05 by https://github.com/NixOS/nixpkgs/pull/156533.
      With the fix, declaring `options.foo.bar` directly works when `foo` is
      already a submodule option. Documented as deprecated in flake-parts in January 2026.
    */
    # :: a -> b
    mkSubmoduleOptions =
      options:
      mkOption {
        # :: a
        type = types.submoduleWith {
          # :: [{ options : a }]
          modules = [{ inherit options; }];
        };
      };

    /**
      Deprecated. Use mkPerSystemType/mkPerSystemOption for `perSystem` type-merging, or
      use Nixpkgs `types.deferredModule` directly, noting the lack of list wrapping;
      see `deferredModuleWith` docs.
    */
    # :: Any -> a
    mkDeferredModuleType = mkLegacyDeferredModuleType;

    /**
      Given a module, construct an option type suitable for type-merging into `perSystem`'s type.
    */
    # :: Any -> a
    mkPerSystemType = mkLegacyDeferredModuleType;

    /**
      Deprecated. Use mkPerSystemOption for `perSystem` type-merging, or
      use `mkOption` and Nixpkgs `types.deferredModule` directly, noting the
      lack of list wrapping; see `deferredModuleWith` docs.
    */
    # :: a -> b
    mkDeferredModuleOption =
      module:
      mkOption {
        # :: a
        type = flake-parts-lib.mkPerSystemType module;
      };

    /**
      Given a module, construct an option declaration suitable for merging into the core `perSystem` module.
    */
    # :: a -> b
    mkPerSystemOption = mkDeferredModuleOption;

    # Polyfill https://github.com/NixOS/nixpkgs/pull/344216
    # Nixpkgs master 2024-12-09, Nixpkgs 25.05
    # :: a
    attrsWith = types.attrsWith or ({ elemType, lazy ? false, placeholder ? "name" }:
      if lazy then types.attrsOf elemType else types.lazyAttrsOf elemType);

    # Helper function for defining a per-system option that
    # gets transposed by the usual flake system logic to a
    # top-level flake attribute.
    # :: { file : c, name : a, option : b } -> { _file : c, config : {}, options : { perSystem : d } }
    mkTransposedPerSystemModule = { name, option, file }: {
      # :: a
      _file = file;

      # :: { perSystem : a }
      options = {
        flake.${name} = mkOption {
          type = attrsWith {
            elemType = option.type;
            lazy = true;
            placeholder = "system";
          };
          default = { };
          description = ''
            See {option}`perSystem.${name}` for description and examples.
          '';
        };

        # :: a
        perSystem = flake-parts-lib.mkPerSystemOption {
          # :: a
          _file = file;

          options.${name} = option;
        };
      };

      # :: {}
      config = {
        transposition.${name} = { };
      };
    };

    # Needed pending https://github.com/NixOS/nixpkgs/pull/198450
    # :: a -> b -> { config : c, options : d } | ... -> { config : f, options : e }
    mkAliasOptionModule = from: to: { config, options, ... }:
      let
        # :: a
        fromOpt = getAttrFromPath from options;
        # :: a
        toOf = attrByPath to
          (abort "Renaming error: option `${showOption to}' does not exist.");
        # :: a
        # :: a
        toType = let opt = attrByPath to { } options; in opt.type or (types.submodule { });
      in
      {
        # :: a
        options = setAttrByPath from (mkOption
          {
            # :: Bool
            visible = true;
            # :: String
            description = "Alias of {option}`${showOption to}`.";
            # :: a -> b
            apply = x: (toOf config);
          } // optionalAttrs (toType != null) {
          # :: a
          type = toType;
        });
        # :: a
        config = mkAliasAndWrapDefsWithPriority (setAttrByPath to) fromOpt;
      };

    # Helper function for importing while preserving module location. To be added
    # in nixpkgs: https://github.com/NixOS/nixpkgs/pull/230588
    # I expect these functions to remain identical. This one will stick around
    # for a while to support older nixpkgs-lib.
    # :: Path -> a -> b
    importApply =
      modulePath: staticArgs:
      lib.setDefaultModuleLocation modulePath (import modulePath staticArgs);

    inherit (import ./lib/memoize/memoize.nix {
      inherit lib;
    }) memoizeStr;

    /**
      `importAndPublish name module` returns a module that both imports the `module`, and exposes it as flake attribute `modules.flake.${name}`.

      This also imports the optional [`modules`](https://flake.parts/options/flake-parts-modules.html) module to support that.
    */
    # :: a -> Path -> { lib : b } | ... -> { _class : "flake", imports : [Path] }
    importAndPublish = name: module: { lib, ... }: {
      # :: "flake"
      _class = "flake";
      # :: [Path]
      imports = [
        module
        ./extras/modules.nix
      ];
      flake.modules.flake.${name} = module;
    };
  };

  # A best effort, lenient estimate. Please use a recent nixpkgs lib if you
  # override it at all.
  # :: "23.05pre-git"
  minVersion = "23.05pre-git";

in

if builtins.compareVersions lib.version minVersion < 0
then
  abort ''
    The nixpkgs-lib dependency of flake-parts was overridden but is too old.
    The minimum supported version of nixpkgs-lib is ${minVersion},
    but the actual version is ${lib.version}${revInfo}.
  ''
else

  flake-parts-lib
